class_name DialogueManager extends Node2D

###	Some commands
### HALT = "%p"
### SUBSTITUTION = "%v"

class _static_signal:
	signal initialized(which: DialogueManager)
	signal condition_changed(which: DialogueManager)

static var static_signal := _static_signal.new()

signal started 
signal finished(is_normal: bool)
@warning_ignore("unused_signal")
signal changed_col(col: int)
@warning_ignore("unused_signal")
signal col_finished(col: int)
@warning_ignore("unused_signal")
signal changed_id(id: int)

@export var json: JSON
@export var activation_method: ACTIVATION_METHOD = ACTIVATION_METHOD.TRIGGER_AREA_ON_CLICK
@export var exitable: bool = true
@export var once: bool = false
	
enum ACTIVATION_METHOD {TRIGGER_AREA_AUTO, TRIGGER_AREA_ON_CLICK, MANUAL}

const UPDATE_TIME: float = 2.0
const DELAY_BEFORE_RAGAINS_CONTROLS: float = 0.01
const SPEED_MULTIPLIER: float = 3.0

var dialogue: Dialogue:
	get: return Dialogue.instance
var interacted := false
var is_started := false

var _trigger_area: Area2D:
	get: return get_node_or_null("TriggerArea")
var _conditions := {}
var _variable_substitutes: Array

func update_conditions(conditions: Dictionary) -> void:
	_conditions.assign(conditions)
	static_signal.condition_changed.emit(self)
	
func update_variable_substitutes(variable_substitutes: Array) -> void:
	_variable_substitutes = variable_substitutes
	
func start_dialogue() -> void:
	if json == null:
		printerr("JSON is empty")
		return
	_setup_dialogue()
	var ids :Array[int] = dialogue.get_ids_from_conditions(_conditions)
	assert(ids.size() > 0, "Dialogue is empty.")
	dialogue.start(ids, self)
	Player.enable_input = false
	dialogue.exitable = exitable
	is_started = true
	started.emit()

func exit_dialogue(is_normal: bool = true) -> void:
	dialogue.exit(is_normal)
	(func():
		Player.enable_input = true
		is_started = false
	).call_deferred()

func get_condition() -> Dictionary:
	return _conditions

func _ready() -> void:
	_setup_signals()
	if json == null:
		printerr("JSON is empty")
		return
	if dialogue == null:
		printerr("Dialogue instance cannot be found")
		return
	static_signal.initialized.emit(self)

func _input(event: InputEvent) -> void:
	if event.is_action_pressed(&"ui_cancel") and dialogue.is_open and is_started:
		if !exitable:
			return
		exit_dialogue(false)
	if activation_method != ACTIVATION_METHOD.TRIGGER_AREA_ON_CLICK:
		return
	if _trigger_area == null:
		return
	if !Player.instance.is_on_floor():
		return
	if event.is_action_pressed(&"ui_down") and _trigger_area.has_overlapping_bodies() and Player.enable_input:
		start_dialogue()
		
func _setup_signals() -> void:
	finished.connect(_dialogue_finished)
	if activation_method == ACTIVATION_METHOD.MANUAL:
		return
	if _trigger_area == null:
		return
	_trigger_area.body_entered.connect(_entered)
	_trigger_area.body_exited.connect(_exited)

func _setup_dialogue() -> void:
	dialogue.speed_multiplier = SPEED_MULTIPLIER
	dialogue.variable_substitutes = _variable_substitutes
	dialogue.json_read(json)

func _dialogue_finished(is_normal: bool) -> void:
	if is_normal:
		interacted = true
	if once:
		queue_free.call_deferred()
	exit_dialogue(is_normal)
	is_started = false

func _exited(_body: Node2D) -> void:
	if exitable:
		exit_dialogue(false)

func _entered(_body: Node2D) -> void:
	if activation_method == ACTIVATION_METHOD.TRIGGER_AREA_AUTO:
		start_dialogue()
