extends Node

signal finished
signal changed_col(col: int)

const HALT := "%p"
const VAR_SUBSTITUTION := "%v"

var text_displayer:
	get: return %TextDisplayer
var skip_button: Button:
	get: return %Skip
var portrait_container: CenterContainer:
	get: return %PortraitContainer
var portrait: TextureRect:
	get: return %Portrait
var name_label: Label:
	get: return %Name
	
var arr_text: Array
var displayed_text: String
var arr_text_index: int
var character: String
var expression: String
var variables: Array:
	get: return (owner as Dialogue).variable_substitutes
var skip_cooldown: SceneTreeTimer
var old_portrait: Texture2D
var halts: Array[int]
	
func start(_arr_text: Array, _character: String, _expression: String) -> void:
	character = _character
	expression = _expression
	arr_text_index = 0
	arr_text = _arr_text
	_create_space_at_the_end_of_every_str_arr()
	displayed_text = arr_text[arr_text_index]
	_set_name_label()
	_set_portrait()
	_set_sfx()
	_animate_in_portrait.call_deferred()
	_start_displayer()
	changed_col.emit(arr_text_index)
	
func exit() -> void:
	text_displayer.exit()

func _ready() -> void:
	text_displayer.finished.connect(_text_finished)
	skip_button.button_down.connect(_skip)

func _text_finished() -> void:
	owner.col_finished.emit(arr_text_index)
	await text_displayer.next
	skip_cooldown = get_tree().create_timer(0.1)
	if text_displayer.active:
		return
	arr_text_index += 1
	if arr_text_index >= arr_text.size():
		finished.emit()
		return
	displayed_text = arr_text[arr_text_index]
	_start_displayer()
	changed_col.emit(arr_text_index)

func _create_space_at_the_end_of_every_str_arr() -> void:
	# a little bit of hack
	for s in arr_text:
		if s.is_empty(): continue
		if s[s.length() - 1] != " ":
			var i := arr_text.find(s)
			arr_text[i] += " "

func _start_displayer() -> void:
	_get_halts()
	_variable_substitution()
	text_displayer.start(displayed_text, halts)

func _variable_substitution() -> void:
	var subs_pos: Array[int] = []
	var old_text := displayed_text
	if variables.is_empty():
		return
	while displayed_text.contains(VAR_SUBSTITUTION):
		subs_pos.append(displayed_text.find(VAR_SUBSTITUTION))
		displayed_text = displayed_text.erase(subs_pos.back(), 2)
	for i in range(subs_pos.size()):
		assert(variables.size() > i, "Variable substitution missmatch at string: '%s'" % old_text)
		displayed_text = displayed_text.insert(subs_pos[i], str(variables[i]))

func _get_halts() -> void:
	halts = []
	while displayed_text.contains(HALT):
		halts.append(displayed_text.find(HALT))
		displayed_text = displayed_text.erase(halts.back(), 2)

func _set_name_label() -> void:
	var upped_name := character.to_upper()
	var name_container = name_label.get_parent()
	if upped_name == "":
		name_label.text = ""
		name_container.hide()
	else:
		name_container.show()
		name_label.text = character

func _set_portrait() -> void:
	var upped_name := character.to_upper()
	var upped_expression := expression.to_upper()
	if upped_name == "" or !_is_expression_exist(upped_name, upped_expression):
		portrait.texture = null
		portrait_container.hide()
		return
	var tex_path = Dialogue.persons[upped_name][upped_expression]
	old_portrait = portrait.texture
	portrait.texture = load(tex_path)
	portrait_container.show()

func _set_sfx() -> void:
	pass
	#var upped_name := character.to_upper()
	#if _is_name_exist(upped_name):
	#	Characters.SFX = Characters.characters[upped_name].SFX

func _animate_in_portrait() -> void:
	if portrait.texture == null:
		return
	if old_portrait == portrait.texture:
		return
	var port_parent := portrait.get_parent()
	port_parent.modulate.a = 0.0
	port_parent.position.x -= 30.0
	AutoTween.new(port_parent, "position", Vector2.ZERO, 0.5)
	AutoTween.new(port_parent, "modulate:a", 1.0, 0.5)

func _is_name_exist(_name) -> bool:
	return Dialogue.persons.has(_name)

func _is_expression_exist(_name: String, _expression: String) -> bool:
	if !_is_name_exist(_name):
		return false
	return Dialogue.persons[_name].has(_expression)

func _skip() -> void:
	if text_displayer.rich_label.text.length() == displayed_text.length():
		text_displayer.next.emit()
		return
	if skip_cooldown != null and skip_cooldown.time_left != 0.0:
		return
	text_displayer.skip()
