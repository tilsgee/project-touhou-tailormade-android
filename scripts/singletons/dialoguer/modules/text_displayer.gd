extends Node

signal finished
signal next

const MAX_TEXT_BOX_SIZE: float = 104.0
const BLINK := "█"
const SPEED_FAST := 1.0/50.0
const SPEED_SLOW := 1.0/15.0

var _MIN_TEXT_BOX_SIZE: float
var _INCREMENT_TEXT_BOX_SIZE: float

var active: bool = true
var rich_label: RichTextLabel:
	get: return %Text
	
#var _speed_multiplier: float:
#	get: return owner.speed_multiplier
var _font_size: float:
	get: return rich_label.get("theme_override_font_sizes/normal_font_size") + 4.0
var _v_spacing: float:
	get: return rich_label.get("theme_override_constants/line_separation")

var _sfx: AudioStreamPlayer
var _blinker: Timer
var _blink: bool = true
var _original_text: String
var _text: String
var _text_index: int
var _text_length: int
var _tps_timer: Timer
var _delay_timer: SceneTreeTimer
var _play_interval: int:
	set(val):
		_play_interval = wrapi(val, 0, 2 if (_tps_timer != null and _tps_timer.wait_time == SPEED_SLOW) else 3)
		if _play_interval == 0 and _sfx != null and active:
			_sfx.play()
var _halts: Array[int]

func start(_displayed_text: String, __halts: Array[int]) -> void:
	active = true
	_text = _displayed_text
	_original_text = _displayed_text
	_halts = __halts
	_text_index = 0
	rich_label.text = ""
	_text_length = _text.length()
	
	_MIN_TEXT_BOX_SIZE = _font_size + _v_spacing
	_INCREMENT_TEXT_BOX_SIZE = _font_size + _v_spacing
	rich_label.custom_minimum_size.y = _MIN_TEXT_BOX_SIZE
	_setup_sfx()
	_setup_timer()
	_connect_timer()

func exit() -> void:
	active = false
	if _tps_timer != null:
		_tps_timer.stop()
	if _delay_timer != null:
		_delay_timer.timeout.emit()
	finished.emit()
	_disconnect_timer()

func skip() -> void:
	rich_label.text = ""
	for i in range(_original_text.length()):
		_original_text = _add_str_by_checking_if_the_word_after_the_next_white_space_will_create_a_new_line__if_so_replace_the_white_space_with_newline(_original_text, i)
		rich_label.text += _original_text[i]
	_update_label_minimum_size()
	_add_blinker()
	exit()
	
func _input(event: InputEvent) -> void:
	if !owner.is_open:
		return
	if owner.is_paused:
		return
	for i in Dialogue.inputs.next:
		if event.is_action_pressed(i):
			next.emit()
			if _delay_timer != null:
				_delay_timer.timeout.emit()
			
func _physics_process(_delta: float) -> void:
	if !owner.is_open:
		return
	if owner.is_paused:
		return
	for i in Dialogue.inputs.fast_forward:
		if Input.is_action_pressed(i):
			_tps_timer.wait_time = SPEED_FAST #/ _speed_multiplier
		if Input.is_action_just_released(i):
			_tps_timer.wait_time = SPEED_SLOW #/ _speed_multiplier
		
func _display() -> void:
	_text = _add_str_by_checking_if_the_word_after_the_next_white_space_will_create_a_new_line__if_so_replace_the_white_space_with_newline(_text, _text_index)
	_update_label_minimum_size()
	if _text_index >= _text.length():
		_tps_timer.stop()
		finished.emit()
		active = false
		return
	var tmp_text := _text[_text_index]
	rich_label.text += tmp_text
	
	if _halts.has(_text_index):
		_pause_timer(1.8)

	_text_index += 1
	_play_interval += 1
	
	if _text_index >= _text_length - 1: # Includes end space
		_tps_timer.stop()
		finished.emit()
		active = false
		_add_blinker()

func _setup_sfx() -> void:
	if _sfx == null:
		_sfx = AudioStreamPlayer.new()
		#_sfx.bus = Audio.get_bus_str(Audio.AUDIO_BUS.UI)
		_sfx.max_polyphony = 1
		owner.add_child(_sfx)
	#_sfx.stream = load(Characters.SFX)

func _setup_timer() -> void:
	if _tps_timer == null:
		_tps_timer = Timer.new()
		add_child(_tps_timer)
		_tps_timer.autostart = true
		_tps_timer.one_shot = false
	_tps_timer.wait_time = SPEED_SLOW# / _speed_multiplier
	_tps_timer.start()

func _connect_timer() -> void:
	if !_tps_timer.timeout.is_connected(_display_2_times):
		_tps_timer.timeout.connect(_display_2_times)
	
func _disconnect_timer() -> void:
	if _tps_timer.timeout.is_connected(_display_2_times):
		_tps_timer.timeout.disconnect(_display_2_times)

func _display_2_times() -> void:
	_display()
	if active and !_tps_timer.is_stopped():
		_display()

func _pause_timer(duration: float):
	_tps_timer.stop()
	_delay_timer = get_tree().create_timer(duration)
	await _delay_timer.timeout
	_tps_timer.start()

# Don't change this, and don't ask any questions.
func _add_str_by_checking_if_the_word_after_the_next_white_space_will_create_a_new_line__if_so_replace_the_white_space_with_newline(text: String, index: int) -> String:
	var space_end_i := text.find(" ", index + 1) + 1
	var old_text := rich_label.text
	var new_temp_text := ""
	var old_line_count := rich_label.get_line_count()
	var new_line_count := 0
	# Temporary put the next word to the __text
	new_temp_text = text.left(space_end_i)
	rich_label.text = new_temp_text
	# Get the line count
	new_line_count = rich_label.get_line_count()
	# Revert back to the old _text
	rich_label.text = old_text
	# See if it creates a new line. If so, put a new line command before the word starts
	if new_line_count - old_line_count == 1 and text[index] == " ":
		text[index] = "\n"
	return text
	
func _update_label_minimum_size() -> void:
	var size_y := clampf(_INCREMENT_TEXT_BOX_SIZE * rich_label.get_line_count(), _MIN_TEXT_BOX_SIZE, MAX_TEXT_BOX_SIZE)
	#var size_y := clampf(min_size * rich_label.get_line_count(), _MIN_TEXT_BOX_SIZE, MAX_TEXT_BOX_SIZE)
	rich_label.custom_minimum_size.y = size_y

func _add_blinker() -> void:
	var length := rich_label.text.length() - 1
	if rich_label.text[length] == " ":
		rich_label.text[length] = BLINK
		_do_blink()
		return
	rich_label.text += BLINK
	_do_blink()

func _do_blink() -> void:
	if _blinker != null:
		_blink = true
		_blinker.start()
		return
	_blinker = Timer.new()
	add_child(_blinker)
	_blinker.one_shot = false
	_blinker.wait_time = 0.5
	_blinker.timeout.connect(func ():
		if rich_label.text == "" or rich_label.text == null:
			return
		var length := rich_label.text.length() - 1
		if active:
			_stop_blinker()
			return
		_blink = !_blink
		rich_label.text[length] = BLINK if _blink else " "
		)
	_blinker.start()

func _stop_blinker() -> void:
	if _blinker != null:
		_blinker.stop()
