extends Node

signal dialogue_finished
signal changed_id(id: int)
signal changed_col(col: int)

var dict_ids: Array

var _text_processor:
	get: return %TextProcessor
var _conditions: Dictionary
var _json_dict: Dictionary
var _id_playlist: Array[int] = [0]
var _current_id: int = 0
var _text: Array
var _character: String
var _expression: String

func start(ids: Array[int]) -> void:
	_id_playlist = ids
	_play_conversation()

func read(json: JSON) -> void:
	_json_dict = _read_json_as_dict(json)
	_reset_curr_id()
	_retreive_attributes_from_dict(_json_dict)

func exit() -> void:
	_text_processor.exit()
	
func _ready() -> void:
	_text_processor.finished.connect(_text_processor_finished)
	_text_processor.changed_col.connect(func(col): changed_col.emit(col))

func _read_json_as_dict(_json: JSON) -> Dictionary:
	return _json.data

func _play_conversation() -> void:
	_retreive_attributes_from_dict(_json_dict)
	_text_processor.start(_text, _character, _expression)
	
func _retreive_attributes_from_dict(dict: Dictionary) -> void:
	var curr_i := _id_playlist[_current_id]
	dict_ids = dict.id
	if (dict.id as Array).size() <= curr_i:
		curr_i = 0
	_text = dict.id[curr_i].text
	_character = dict.id[curr_i].character
	_conditions = dict.id[curr_i].conditions
	_expression = dict.id[curr_i].expression

func _reset_curr_id() -> void:
	_current_id = 0
	changed_id.emit(_current_id)

func _text_processor_finished() -> void:
	_current_id += 1
	if _current_id >= _id_playlist.size():
		dialogue_finished.emit()
		return
	_play_conversation()
	changed_id.emit(_current_id)
